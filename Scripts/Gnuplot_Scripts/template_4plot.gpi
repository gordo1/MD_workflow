# Text File
# AUTHOR:   Shane Gordon
# FILE:     template.gpi
# ROLE:     TODO (some explanation)
# CREATED:  2014-09-09 10:10:34
# MODIFIED: 2015-05-26 11:36:29

set style data lines
set encoding iso_8859_1

# Enable macros
set macros

# Border options
BORDERWIDTH = '5'
set border linewidth BORDERWIDTH

# Font options
axis_font = "Arial,12pt"
label_font = "Arial,12pt"

# Key
set key opaque

# Style defaults
set style line 1 lc rgb '#0060ad' lt 1 lw 2 pt 5    #blue
set style line 2 lc rgb '#dd181f' lt 1 lw 2 pt 7    #red
set style line 3 lc rgb '#5e9c36' lt 1 lw 2 pt 9    #green
set style line 4 lc rgb "orange" lt 1 lw 2 pt 11   #orange
BLUE = "1"
RED = "2"

# Fit Stuff
set fit quiet
set fit logfile '/dev/null'
set fit errorvariables

# Plot 1
set term pdfcairo enhanced color dashed size 8.27in,11.69in
set output OUTPUT
set multiplot
set size 0.5,0.33
set origin 0,0.66
set xtics out scale 1 font axis_font nomirror # Removes ytics on RHS
set ytics out scale 1 font axis_font nomirror # Removes xtics on top
set xlabel XLABEL1 font label_font
set ylabel YLABEL1 font label_font
set yrange [0:100]
plot SECONDARYSTRUCTURE u ($0):($1*100+$2*100+$3*100+$4*100) w filledcurves x1 ls 4 title TITLE1p4, \
SECONDARYSTRUCTURE u ($0):($1*100+$2*100+$3*100) w filledcurves x1 ls 3 title TITLE1p3, \
SECONDARYSTRUCTURE u ($0):($1*100+$2*100) w filledcurves x1 ls 2 title TITLE1p2, \
SECONDARYSTRUCTURE u ($0):($1*100) w filledcurves x1 ls 1 title TITLE1p1, \
SECONDARYSTRUCTURE u ($0):($1*100+$2*100+$3*100+$4*100) w l ls 4 lc rgb "black" notitle, \
SECONDARYSTRUCTURE u ($0):($1*100+$2*100+$3*100) w l ls 3 lc rgb "black" notitle, \
SECONDARYSTRUCTURE u ($0):($1*100+$2*100) w l ls 2 lc rgb "black" notitle, \
SECONDARYSTRUCTURE u ($0):($1*100) w l ls 1 lc rgb "black" notitle

# Plot 5
set size 0.5,0.33
set origin 0.5,0.66
set xtics out scale 1 font axis_font nomirror # Removes ytics on RHS
set ytics out scale 1 font axis_font nomirror # Removes xtics on top
set xlabel XLABEL5 font label_font
set ylabel YLABEL5 font label_font
set yrange [0:*]
plot FILE5 u 1:2 w l ls 1 title TITLE5

# Plot 2
set size 0.5,0.33
set origin 0,0.33
set xtics out scale 1 font axis_font nomirror # Removes ytics on RHS
set ytics out scale 1 font axis_font nomirror # Removes xtics on top
set xlabel XLABEL2 font label_font
set ylabel YLABEL2 font label_font
set yrange [0:*]
rg_fit(x) = offset
offset = 1
gradient = 1e-2
fit rg_fit(x) FILE2 u 1:2 every 100 via offset
set label sprintf("plateau \@ approx %.1f {\261} %.2f {\305}", offset, offset_err) at graph 0.1,0.9 font label_font
plot FILE2 w l ls 1 title TITLE2,\
rg_fit(x) w l lt 9 lc rgb "red" title "fit"
unset label 1

# Plot 3
set size 0.5,0.33
set origin 0.5,0.33
set xtics out scale 1 font axis_font nomirror # Removes ytics on RHS
set ytics out scale 1 font axis_font nomirror # Removes xtics on top
set xlabel XLABEL3 font label_font
set ylabel YLABEL3 font label_font
set yrange [0:*]
set fit quiet
set fit logfile '/dev/null'
rmsd_fit(x) = asymptote*(1-exp(-(kappa)*x))
asymptote = 1e-6
kappa = 0.01
fit rmsd_fit(x) FILE3 u 1:2 every 100 via kappa, asymptote
set label sprintf("plateau at approx %.1f {\261} %.2f {\305}", asymptote, asymptote_err) at graph 0.1,0.9 font label_font
plot FILE3 u 1:2 w l ls 1 title TITLE3,\
rmsd_fit(x) w l lt 9 lc rgb "red" title "fit"
unset label 1

# Plot 4
set size 1,0.33
set origin 0,0
set xtics out scale 1 font axis_font nomirror # Removes ytics on RHS
set ytics out scale 1 font axis_font nomirror # Removes xtics on top
set xlabel XLABEL4 font label_font
set ylabel YLABEL4 font label_font
set yrange [0:*]
plot for [i=1:5] ''.raw.'/rmsf_protein_backbone_'.i.'of5_'.index_no.'.txt' w l title ''.i.'th Fraction of 5'

reset
